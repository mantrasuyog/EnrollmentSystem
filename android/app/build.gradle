apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"
apply plugin: "com.google.gms.google-services"

react {

    hermesFlags = ["-O", "-output-source-map"]

    autolinkLibrariesWithApp()
}

def enableProguardInReleaseBuilds = true

def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.enrollmentsystem"
    defaultConfig {
        applicationId "com.enrollmentsystem"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        manifestPlaceholders = [ usesCleartextTraffic: "true" ]

        // Only include arm64-v8a native libraries - reduces APK size significantly
        ndk {
            abiFilters "arm64-v8a"
        }
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            if (project.hasProperty('MYAPP_UPLOAD_STORE_FILE')) {
                storeFile file(MYAPP_UPLOAD_STORE_FILE)
                storePassword MYAPP_UPLOAD_STORE_PASSWORD
                keyAlias MYAPP_UPLOAD_KEY_ALIAS
                keyPassword MYAPP_UPLOAD_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled enableProguardInReleaseBuilds
            shrinkResources enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions {
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/proguard/androidx-*.pro'
        exclude 'META-INF/com.android.tools/proguard/androidx-*.pro'
    }
}

dependencies {

    implementation("com.facebook.react:react-android")

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }

    // Tech5 Fingerprint SDK
    implementation project(path: ':AirsnapFinger')
    implementation project(path: ':AirsnapFingerUI')
    implementation project(path: ':Ncnn_CPP_20230816')
    implementation project(path: ':OpenCV_CPP_460')

    // Tech5 Face SDK
    implementation project(path: ':AirsnapFaceCore')
    implementation project(path: ':AirsnapFaceUI')

    // CameraX dependencies for Tech5 finger capture
    def camerax_version = '1.5.1'
    implementation "androidx.camera:camera-core:$camerax_version"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"

    // Additional dependencies for Tech5
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'com.google.guava:guava:32.1.3-android'
}
